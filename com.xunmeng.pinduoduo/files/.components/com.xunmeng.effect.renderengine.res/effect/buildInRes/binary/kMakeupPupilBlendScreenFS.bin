
precision highp float;
varying vec2 textureCoordinate;
varying vec2 sucaiTextureCoordinate;
//varying float varyingOpacity;

uniform sampler2D inputImageTexture;
uniform sampler2D inputImageTexture2;
uniform sampler2D maskImageTexture;
uniform sampler2D reflectImageTexture;

uniform float intensity;
uniform float reflectIntensity;
uniform int reflectEnable;

#ifndef BLEND_SCREEN
#define BLEND_SCREEN

float blendScreen(float base, float blend) {
    return 1.0 - ((1.0 - base) * (1.0 - blend));
}

vec3 blendScreen(vec3 base, vec3 blend) {
    return vec3(blendScreen(base.r, blend.r), blendScreen(base.g, blend.g), blendScreen(base.b, blend.b));
}

vec3 blendScreen(vec3 base, vec3 blend, float opacity) {
    return (blendScreen(base, blend) * opacity + blend * (1.0 - opacity));
}

#endif

#ifndef BLEND_SOFTLIGHT
#define BLEND_SOFTLIGHT

float blendSoftLight(float base, float blend) {
    return (blend < 0.5) ? (2.0 * base * blend + base * base * (1.0 - 2.0 * blend)) : (sqrt(base) * (2.0 * blend - 1.0) + 2.0 * base * (1.0 - blend));
}

vec3 blendSoftLight(vec3 base, vec3 blend) {
    return vec3(blendSoftLight(base.r, blend.r), blendSoftLight(base.g, blend.g), blendSoftLight(base.b, blend.b));
}

vec3 blendSoftLight(vec3 base, vec3 blend, float opacity) {
    return (blendSoftLight(base, blend) * opacity + base * (1.0 - opacity));
}

#endif

#define blendModel blendSoftLight
#define blendReflect blendScreen

void main(void)
{
    vec4 src = texture2D(inputImageTexture, textureCoordinate);
    vec4 sucai = texture2D(inputImageTexture2, sucaiTextureCoordinate);
    vec4 mask = texture2D(maskImageTexture, textureCoordinate);
    sucai.a = min(sucai.a, mask.r);

    vec3 color = blendModel(src.rgb, clamp(sucai.rgb, 0.0, 1.0));
    color = mix(src.rgb, color, sucai.a * intensity);

    if (reflectEnable == 1)
    {
        vec4 reflect = texture2D(reflectImageTexture, sucaiTextureCoordinate);
        vec3 tmp = blendReflect(color.rgb, clamp(reflect.rgb, 0.0, 1.0));
        reflect.a = step(0.001, sucai.a);
        color = mix(color, tmp, reflect.a * reflectIntensity);
    }

    gl_FragColor = vec4(color, 1.0);
}
